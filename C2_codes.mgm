// This function generates the parity-check matrix of 
// an LDPC code from the incidence structure on the blown up plane
// given by the linear system C_2 of hyperbolas with two
// common points at infinity.

// These codes are described in the article
// "Incidence structures from the blown-up plane
// and LDPC codes". IEEE Trans. Inform. Theory 57(7), 4401-4416, 2011.

//-----------------------------------------------------------
// INPUTS: The Size q of the base field of the affine plane.
// OUTPUTS: The sparse matrix.
//-----------------------------------------------------------

load "Slin.mgm";

function C2code(q)
	 F:=FiniteField(q);

	 // Construt a basis of the set of curves.
	 A2<x,y>:=AffineSpace(F,2);
	 LSec:=[x*y,x,y,1];

	 // The set parametrizing the set of curves.
	 P3<u,v,w,t>:=ProjectiveSpace(F,3);
	 R:=Scheme(P3,u*(v*w-t*u));
	 ParaLS:=RationalPoints(P3) diff RationalPoints(R);
	 

	 // Construct the set of possible tangent vectors.
	 P1<X,Y>:=ProjectiveSpace(F,1);
	 Q:=P1![0,1];
	 Qbis:=P1![1,0];
	 VectTgts:=RationalPoints(P1) diff {@ Q, Qbis @};

	 H:=IncidenceSyst(A2,LSec, ParaLS, 2, VectTgts);

	 return H;
end function;